export malloc  (m nat) @none
export strtoul (p @byte q @@byte b nat) nat

/* end preamble */

type
  TokenKind @()none

  Token {
    s    @Token
    q     TokenKind
    p    @byte
    n     nat
    l     nat
    line  nat
    col   nat
  }

  Type {
    s      @Type
    size    nat
    align   nat
    offset  nat
    o      @Type
    a      @Type
    params @Type
    k   @Token
    format @(t @Type s @byte) none
  }

  Expr {
    s @Expr
    a @Expr
    k @Token
    t @Type
    u @Type
    sp nat
    gen @(e @Expr c nat l nat) nat
  }

  Scope {
    s @Scope
    k @Token
    t @Type
    e @Expr
    is_type  nat
    is_def   nat
  }

  C byte
  T @Type
  O @Scope
  K @Token
  E @Expr
  N nat
  S @byte

  ftfn (t T s S) none

  tx   (k @K o O        ) T
  ex   (k @K o O        ) E
  exa  (k @K o O     a E) E
  exk  (k @K o O k1 K    ) E
  exka (k @K o O k1 K a E) E
  necl (e E c N l N) N

  gelfn (v N) N
  geefn (e E c N l N gop @() none) N
  gegfn () none
;

ftnone ftfn ftbyte ftfn
ftnat  ftfn ftstct ftfn ftptr  ftfn
ftfunc ftfn

p2     ex p3     ex p5     ex p7     ex
pmul   ex padd   ex psh    ex pcmp   ex

ptype  tx

gelit   necl gesize  necl gederef necl geaddr  necl gecast  necl geread  necl gewrite  necl geerr  necl
geid    necl gelnot  necl geneg   necl gediv   necl gemod   necl geexit  necl gescratch necl
gee     necl gene    necl geb     necl gebe    necl gea     necl geae    necl
geadd   necl gesub   necl gemul   necl gexor   necl geass   necl gestr   necl
geshl   necl geshr   necl geelem  necl gemem   necl geseq   necl geret   necl
gecond  necl geland  necl gelor   necl geis    necl gefor   necl gecall  necl

esize  exk ecast  exk enat   exk eaddr  exk escratch exk
eneg   exk elnot  exk eid    exk estr   exk ebrace exk eparen exk eexit  exk ebyte  exk eread exk ewrite exk eerr exk

eas    exka eelem  exka ederef exka eseq   exka
eret   exka emul   exka ediv   exka emod   exka ethen  exka
eelse  exka econd  exka eadd   exka esub   exka eshl   exka
eshr   exka eb     exka ea     exka ebe    exka eae    exka
ee     exka ene    exka eland  exka ecall  exka emem   exka
elor   exka eass   exka eis    exka efor   exka

JID  () none = 0
JPCT () none = 0
JCHR () none = 0
JSTR () none = 0
JNUM () none = 0

isc (c C d C) nat = (c == d)

issp (c C    ) nat = isc c ' '
istb (c C    ) nat = isc c '\t'
islf (c C    ) nat = isc c '\n'
iscr (c C    ) nat = isc c '\r'
is_  (c C    ) nat = isc c '_'

isin(c C a C b C) nat = (a <= c and c <= b)

isAZ (c C    ) nat = isin c 'A' 'Z'
isaz (c C    ) nat = isin c 'a' 'z'
is09 (c C    ) nat = isin c '0' '9'
isaf (c C    ) nat = isin c 'a' 'f'

isws (c C    ) nat = {
   issp c else
   istb c else
   islf c else
   iscr c
}
is0f (c C    ) nat = {
   is09 c else
   isaf c
}
isid (c C    ) nat = {
   isAZ c else
   isaz c else
   is09 c else
   is_  c
}

memcmp2(a S b S n N) nat = {
  1 is r
  { n then n for m
    (a@ == b@) ?
      { (a = &a^1)
        (b = &b^1)
        (m - 1) }
      { (r = 0)
        0 } }
  r
}

strncmp2(a S b S n N) nat = {
  1 is r
  { n then
    n for m
    (a@ == b@) ?
      { a@ then
        (a = &a^1)
        (b = &b^1)
        (m - 1) }
      { (r = 0)
        0 } }
  r
}

strcmp2(a S b S) nat = {
  1 is r
  { 1 for _
    (a@ == b@) ?
      { a@ then
        (a = &a^1)
        (b = &b^1)
        1 }
      { (r = 0)
        0 } }
  r
}

sw1(p S a C    ) nat = {(p^0 == a) then 1}
sw2(p S a C b C) nat = {(p^0 == a) then (p^1 == b) then 2}

read_punct(p S) N = {
  sw2 p '=' '=' else
  sw2 p '!' '=' else
  sw2 p '<' '=' else
  sw2 p '>' '=' else
  sw2 p '<' '<' else
  sw2 p '>' '>' else
  sw2 p '/' '/' else
  sw1 p '='     else
  sw1 p '<'     else
  sw1 p '>'     else
  sw1 p '+'     else
  sw1 p '-'     else
  sw1 p '*'     else
  sw1 p '/'     else
  sw1 p '&'     else
  sw1 p '|'     else
  sw1 p '@'     else
  sw1 p '#'     else
  sw1 p '{'     else
  sw1 p '}'     else
  sw1 p '('     else
  sw1 p ')'     else
  sw1 p '['     else
  sw1 p ']'     else
  sw1 p '!'     else
  sw1 p '?'     else
  sw1 p ':'     else
  sw1 p ';'     else
  sw1 p ','     else
  sw1 p '.'     else
  sw1 p '~'     else
  sw1 p '^'     else
  sw1 p '$'     else
  sw1 p '`'     else
  sw1 p '%'
}

export stdout () none
export stderr () none
export stdin  () none

strlen2(a S) N = {
  0 for i a^i then (i + 1)
}

gc (c C) none = write &c 1
gs (s S) none = write s strlen2 s
gsn(s S n N) none = write s n
glf() none = gc '\n'
gn1(n N) none = { n then gn1 (n / 10) gc cast byte ('0' + n % 10) }
gn (n N) none = { n ? gn1 n gc '0' }

oc (c C) none = err &c 1
os (s S) none = err s strlen2 s
osn(s S n N) none = err s n
olf(   ) none = oc '\n'
on1(n N) none = { n then on1 (n / 10) oc cast byte ('0' + n % 10) }
on (n N) none = { n ? on1 n oc '0' }

align(n N a N) N = ((n + a - 1) / a * a)

diag (line N col N s S) none = { oc '(' on line oc ',' on col os "): " os s olf exit 1 }
kdiag(k K s S) none = { diag k.line k.col s }

type_equal(t T u T) nat = {
  t then
  u then
  (t == u) else
  t.o ? type_equal t.o u
  u.o ? type_equal t u.o
  { (t.format == u.format) then
    (t.format == &ftnone ) else
    (t.format == &ftbyte ) else
    (t.format == &ftnat  ) else
    (t.format == &ftptr  ) ? type_equal t.a u.a
      { (t.format == &ftfunc ) then
        t.a is p
        u.a is q
        { 1 for _
          type_equal p q then
          (p = p.s)
          (q = q.s)
          1 }
        (p == q) } }
}

ntype(size N align N format @ftfn) T = {
  cast T malloc sizeof Type is t
  (t.size  = size  )
  (t.align = align )
  (t.format = format)
  (t.s = 0)
  t
}

ntypea(size N align N format @ftfn a T) T = {
  ntype size align format is t
  (t.a = a)
  t
}

nnone (   ) T = ntype  0 0 &ftnone
nbyte (   ) T = ntype  1 1 &ftbyte
nnat  (   ) T = ntype  8 8 &ftnat
nstct (   ) T = ntype  0 1 &ftstct
nptr  (a T) T = ntypea 8 8 &ftptr  a
nfunc (a T) T = ntypea 1 1 &ftfunc a

tree(k K t T gen @necl) E = {
  cast E malloc sizeof Expr is e
  (e.k = k)
  (e.t = t)
  (e.gen = gen)
  e
}

t0  (k K t T gen @necl            ) E = {tree k t gen}
t1  (k K t T gen @necl         a E) E = {tree k t gen is e e ret (e.a = a)}
t2  (k K t T gen @necl     b E a E) E = {tree k t gen is e e ret (e.a = a)  (e.a.s = b)}
t3  (k K t T gen @necl c E b E a E) E = {tree k t gen is e e ret (e.a = a)  (e.a.s = b)  (e.a.s.s = c)}
tt  (k K t T gen @necl u T        ) E = {t0   k t gen is e e ret (e.u = u)}

ntoken(q TokenKind a S b S line N col N) K = {
  cast K malloc sizeof Token is k
  (k.s    = 0    )
  (k.q    = q    )
  (k.p    = a    )
  (k.n    = b - a)
  (k.line = line )
  (k.col  = col  )
  k
}

read_file() S = {
  (1 << 20) is big
  cast @byte malloc big is buf
  read buf big is n
  (buf^n = '\0')
  buf
}

try_search_scope(o O c S n N) O = {
  cast O 0 is r
  { o then
    o for oi
    (n != oi.k.n or !strncmp2 oi.k.p c n) ?
      oi.s
      { (r = oi)
        cast O 0 } }
  r
}

search_scope(k @K o O c S n N) @Scope = {
  try_search_scope o c n else
  kdiag k@ "lookup failed"
}

jadv   (k @K    ) K    = { k@ ret (k@ = k.s) }
jeq    (k @K c S) nat  = { k@ then memcmp2 k.p c k.n then !c^k.n }
expect (k @K c S) none = { { !jeq k c then kdiag k@ "unexpected" } jadv k }
consume(k @K c S) nat  = { jeq k c is r { r then jadv k } r }

memcpy2 (a S b S n N) none = {
  n then
  n for m
  (a@ = b@)
  (a = &a^1)
  (b = &b^1)
  (m - 1)
}

declarator (k @K o O t @T) none = {
  (t@ = cast T malloc sizeof Type)
  jadv k is k1
  ptype k o is u
  memcpy2 cast S t@ cast S u sizeof Type
  (t.o = u)
  (t.k = k1)
}

struct_decl tx = {
  nstct is t
  &t.a is u
  { 1 for _ 
     !consume k "}" then
      declarator k o u
      (u = &u.s) }
  0 is bits
  { !!t.a then
    t.a for g
    (bits = align bits (g.align * 8))
    (g.offset = bits / 8)
    (bits = bits + g.size * 8)
    {(t.align < g.align) then (t.align = g.align)}
    g.s }
  (t.size = align bits (t.align * 8) / 8)
  t
}

func_params tx = {
  cast T 0 is params
  &params is u
  { 1 for _
    !jeq k ")" then
    declarator k o u
    (u = &u.s)
    1 }
  jadv k
  ptype k o is ret
  nfunc cast T malloc sizeof Type is t
  memcpy2 cast S t.a cast S ret sizeof Type
  (t.a.s = params)
  t
}

ptype tx = {
  consume k "none"   ? nnone
  consume k "byte"   ? nbyte
  consume k "nat"    ? nnat
  consume k "@"      ? nptr        ptype k o
  consume k "("      ? func_params       k o
  consume k "{"      ? struct_decl       k o
  { search_scope k o k.p k.n is s
    (s.is_type or kdiag k@ "not a type")
    jadv k
    s.t }
}

pj(k @K o O     q TokenKind e @exk ) E = {k@ then (k.q == q) then jadv k is k1 e@ k o k1}
pl(k @K o O     c S         e @exk ) E = {k@ then jeq k c then jadv k is k1 e@ k o k1  }
pr(k @K o O a E c S         e @exka) E = {k@ then jeq k c then jadv k is k1 e@ k o k1 a}
pu(k @K o O a E c S         e @exka) E = {k@ then consume k c ? a e@ k o k@ a}

pa1    ex  = { pj    k o   &JID   &eid   else
               pj    k o   &JSTR  &estr  else
               pj    k o   &JNUM  &enat  else
               pj    k o   &JCHR  &ebyte      }
pa3    exa = { (a.t.format == &ftfunc) then ecall k o k@ a }
pa4    exa = { pr    k o a "?"    &econd      }
pa5    exa = { pr    k o a "ret"  &eret  else
               pr    k o a "is"   &eis   else
               pr    k o a "for"  &efor  else
               pr    k o a "then" &ethen else
               pr    k o a "else" &eelse else
               pu    k o a "}"    &eseq       }
pamul  exa = { pr    k o a "*"    &emul  else
               pr    k o a "/"    &ediv  else
               pr    k o a "%"    &emod       }
paadd  exa = { pr    k o a "+"    &eadd  else
               pr    k o a "-"    &esub       }
pash   exa = { pr    k o a "<<"   &eshl  else
               pr    k o a ">>"   &eshr       }
pacmp  exa = { pr    k o a "<"    &eb    else
               pr    k o a ">"    &ea    else
               pr    k o a "<="   &ebe   else
               pr    k o a ">="   &eae   else
               pr    k o a "=="   &ee    else
               pr    k o a "!="   &ene        }
paland exa = { pr    k o a "and"  &eland      }
palor  exa = { pr    k o a "or"   &elor       }
pa7    exa = { pr    k o a "as"   &eas        }

p2 ex = {
  pl k o "&"       &eaddr    else
  pl k o "-"       &eneg     else
  pl k o "!"       &elnot    else
  pl k o "not"     &elnot    else
  pl k o "cast"    &ecast    else
  pl k o "sizeof"  &esize    else
  pl k o "exit"    &eexit    else
  pl k o "read"    &eread    else
  pl k o "write"   &ewrite   else
  pl k o "err"     &eerr     else
  { consume k "(" ?
      { { p7    k o for a
          pr    k o a "=" &eass else
          pa7   k o a
        } ret
        expect k ")"                     }
    pl  k o "{"  &ebrace else
    pa1 k o else
    kdiag k@ "expected an expression"
  } for a
  pr    k o     a "^"       &eelem   else
  pr    k o     a "."       &emem   else
  pr    k o     a "@"       &ederef
}

p3    ex = { p2    k o for a pa3    k o a }
p4    ex = { p3    k o for a pa4    k o a }
p5    ex = { pl    k o "scratch" &escratch else
             p4    k o is  a pa5    k o a }
pmul  ex = { p4    k o for a pamul  k o a }
padd  ex = { pmul  k o for a paadd  k o a }
psh   ex = { padd  k o for a pash   k o a }
pcmp  ex = { psh   k o for a pacmp  k o a }
pland ex = { pcmp  k o for a paland k o a }
plor  ex = { pland k o for a palor  k o a }
p7    ex = { plor  k o for a pa7    k o a }

typedecl(k @K o O) none
function(k @K o O) none

decls(k @K o O) none = {
  k@ then
  (consume k "type" ? &typedecl &function)@ k o
}

typedecl(k @K o O) none = {
  consume k ";" ?
    decls k o
    { scratch Scope o1
      (o1.s = o)
      (o1.k = k@)
      (o1.is_type = 1)
      declarator k &o1 &o1.t
      typedecl k &o1 }
}

argreg1(d N) S = {
  (d == 0) ? "dil"
  (d == 1) ? "sil"
  (d == 2) ? "dl"
  (d == 3) ? "cl"
  (d == 4) ? "r8b"
             "r9b"
}

argreg8(d N) S = {
  (d == 0) ? "rdi"
  (d == 1) ? "rsi"
  (d == 2) ? "rdx"
  (d == 3) ? "rcx"
  (d == 4) ? "r8"
             "r9"
}

gtreg(s S        ) none = { gc '%' gs s }
gt0  (s S        ) none = { gs s glf  }
gt1  (s S a S    ) none = { gs s gc ' ' gtreg a glf }
gt2  (s S a S b S) none = { gs s gc ' ' gtreg a gc ',' gtreg b glf }

gtz (a S) none = gt2  "xor"  a a
gtcz(a S) none = gt2  "test" a a

gtlit(l N a S) none = (!!l ? { gs "mov $" gn l gc ',' gtreg a glf } gtz  a)

gtnot(    a S) none = gt1 "not" a
gtneg(    a S) none = gt1 "neg" a
gmul (    a S) none = gt1 "mul" a
gdiv (       ) none = { gtz "edx" gt1 "div" "rdi" }
gmod (       ) none = { gdiv
                        gt2 "mov" "rdx" "rax" }
gshx (x C    ) none = { gt2 "mov" "dil" "cl" gs "sh" gc x gs " %cl,%rax" glf }

gadd (a S b S) none = gt2 "add" a b
gsub (a S b S) none = gt2 "sub" a b
gand (a S b S) none = gt2 "and" a b
gor  (a S b S) none = gt2 "or"  a b
gxor (a S b S) none = gt2 "xor" a b
gcmp (a S b S) none = gt2 "cmp" a b
gshr () none = gshx 'r'
gshl () none = gshx 'l'

gpromote(b S) none = (gt2 "movzbl" "al" b)

gsetf     (s S     b S    ) none = { gs "set" gs s gc ' ' gtreg "al" glf gpromote b }
gcmp_setf (s S a S b S c S) none = { gcmp a b gsetf s c }
gcmpz_setf(s S a S b S    ) none = { gtcz a   gsetf s b }
glnot     (    a S b S    ) none = { gcmpz_setf "e" a b }

gj        (e E l S    ) none = { gs "jmp " gs l gn cast nat e gc '$' glf }
gjc       (e E l S s S) none = { gs "j" gs s gc ' ' gs l gn cast nat e gc '$' glf }
glabel    (e E l S    ) none = { gs l gn cast nat e gs "$:" glf }

gje  (e E l S) none = gjc e l "e"
gjne (e E l S) none = gjc e l "ne"
gjb  (e E l S) none = gjc e l "b"
gjbe (e E l S) none = gjc e l "be"
gja  (e E l S) none = gjc e l "a"
gjae (e E l S) none = gjc e l "ae"

gegcmp(s S) none = gcmp_setf s "rdi" "rax" "eax"

ge  () none = gegcmp "e"
gne () none = gegcmp "ne"
gb  () none = gegcmp "b"
gbe () none = gegcmp "be"
ga  () none = gegcmp "a"
gae () none = gegcmp "ae"

gcast(a T b T) none = (
  (b.format == &ftnone) ?  gtz     "eax"
  { (b.format == &ftbyte) then
    gpromote  "eax" }
)

gfpush(c @N  l N ) none = { !!l then gs "sub $" gn (l << 3) gc ',' gtreg "rsp" glf (c@ = c@ + l) }
gfpop (c @N  l N ) none = { !!l then gs "add $" gn (l << 3) gc ',' gtreg "rsp" glf (c@ = c@ - l) }
gpush (c @N  d S ) none = { gt1 "push" d (c@ = c@ + 1) }
gpop  (c @N  s S ) none = { gt1 "pop"  s (c@ = c@ - 1) }

gindex(      l N ) none = { !!l then gs  "add $" gn  l gc  ',' gtreg  "rax" glf  }

gaddr(q @ftfn n N s S d S) none = {(!strcmp2 s d or n) then gs  "lea " gn  n gs  "(%r" gs  s gs  "),%r" gs  d glf }
gload(q @ftfn n N s S d S) none = (
  (q == &ftstct or q == &ftfunc) ?
    gaddr  q n s d 
  (q == &ftbyte) ?
    {gs  "movzbl " gn  n gs  "(%r" gs  s gs  "),%e" gs  d glf }
  {gs  "mov " gn  n gs  "(%r" gs  s gs  "),%r" gs  d glf }
)

gaddrg(e E c @N d S) none = { gs "mov " gsn e.t.k.p e.t.k.n gs "@GOTPCREL(%rip),%r" gs d glf }
gaddrl(e E c @N d S) none = { gaddr e.a.u.format (c@ - e.a.sp << 3) "sp" d }
gloadl(e E c @N d S) none = { gload e.a.u.format (c@ - e.a.sp << 3) "sp" d }

gloadsp(t T d S) none = (
  (t.format == &ftstct) ?
    { gs "mov %rsp,%" gs d glf }
  (t.format == &ftbyte) ?
    { gs "movzbq (%rsp), %" gs d glf }
    { gs "mov (%rsp),%" gs d glf }
)

gstoresp(t T) none = {
  (t.format == &ftstct) ?
    { 0 for i
      (i < t.size) then
      (i + 1) ret
      gs "mov " gn i gs "(%rax),%r8b" glf
      gs "mov %r8b," gn i gs "(%rsp)" glf }
  (t.format == &ftbyte) ?
    { gs "mov %al,(%rsp)"  glf }
    { gs "mov %rax,(%rsp)" glf }
}

gstore(t T c @N) none = {
  gpop c "rdi"
  (t.format == &ftbyte) ?
    { gs "mov %al,(%rdi)"  glf }
    { gs "mov %rax,(%rdi)" glf }
}

gexpr necl  = {gc '#' gsn e.k.p e.k.n glf e.gen@ e c l }

gdrop (e E c N    ) none = { cast none gexpr  e c 0 }
geval (e E c N l N) none = {
  gexpr e c l is l1
  !!l1 then
  l1 for l2
  gload ((l2 == 1 and l == 0) ? e.t.format &ftptr) 0 "ax" "ax"
  (l2 - 1)
}

gtpars(k @K o O t T c N g N) N = {
  !t ? { gdrop p4 k o c 0 }
    { scratch Expr nd
      scratch Scope o1
      (o1.s = o)
      (o1.k = t.k)
      (o1.e = &nd)
      (o1.is_type = 0)
      (nd.sp  = -g)
      (nd.u   = t)
      gtpars k &o1 t.s c (g + 1) }
}
gtparr(k @K o O t T c N g N) N = {
  !t ? { gdrop p4 k o c 0 }
    { gpush &c argreg8 g
      scratch Expr nd
      scratch Scope o1
      (o1.s = o)
      (o1.k = t.k)
      (o1.is_type = 0)
      (o1.e = &nd)
      (nd.sp = c)
      (nd.u  = t)
      (((g == 5) ? &gtpars &gtparr)@ k &o1 t.s c (g + 1) + 1) }
}
function(k @K o O) none = {
  consume k "export" is globl
  scratch T t
  declarator k o &t
  try_search_scope o t.k.p t.k.n is o1
  { o1 then
    (o1.is_type and kdiag k@ "redeclared as a different type of symbol")
    (o1.is_def >= jeq k "=" and kdiag k@ "remention") }
  scratch Scope o2
  (o2.s = o)
  (o2.k = t.k)
  (o2.t = t)
  (o2.e = 0)
  (o2.is_type = 0)
  (o2.is_def = jeq k "=")
  { o2.is_def then
    jadv k
    gs ".section .text." gsn t.k.p t.k.n glf
    { globl then gs ".globl" gc ' ' gsn t.k.p t.k.n glf }
    gsn t.k.p t.k.n gc ':' glf
    0 is c
    gfpop &c gtparr k &o2 t.a.s c 0
    gt0 "ret" }
  decls k &o2
}

ecall exka = {
  a.t.a.s is u
  { a for b
    u then
    (b.s = p3 k o)
    { !type_equal b.s.t u then kdiag k@ "asymmetric argument" }
    (u = u.s)
    b.s }
  t1 k@ a.t.a &gecall a
}
gargs(e E c @N) none = {
  !!e then
  gargs e.s c
  geval e c@ 0
  gpush c "rax"
}
gecall necl  = {
  gargs e.a.s &c
  geval e.a    c 0
  0 is d
  { !!e.a.s then
    e.a.s for e (d < 6) then e.s ret
    gpop &c argreg8 d
    (d = d + 1) }
  gs "call *%rax" glf
  0
}

export main(argc N argv @S) N = {
  gt0 ".data"
  read_file is p
  cast K 0 is k
  1 is line
  1 is col
  &k is x
  { 1 for _
    p@ then
    (p^0 == '/' and p^1 == '*') ? {
      (p = &p^2)
      { 1 for _
        (p@ or diag line col "unclosed block comment")
        (p^0 == '*' and p^1 == '/') ?
          { (p = &p^2) 0 }
          { ((p@ == '\n') ? { (line = line + 1) (col = 1) } (col = col + 1))
            (p = &p^1) 1 } } }
    isws p@ ?
      { ((p@ == '\n') ? { (line = line + 1) (col = 1) } (col = col + 1))
        (p = &p^1) }
    { is09 p@ ? {
        { &p^1 for q isid q@ then (col = col + 1) &q^1 } is q
          (x@ = ntoken &JNUM p q line col)
          (x.l = strtoul x.p &x.p 10)
        (p = q) }
      (p@ == '"') ? {
        { &p^1 for q
          (q^0 or diag line col "unclosed string literal")
          (q^0 == '\n' and diag line col "newline in string literal")
          (q^0 == '\\') ?
          { (q^1 or diag line col "unclosed string literal")
            (q^1 == '\n' and diag line col "newline in string literal")
            (col = col + 2)
            &q^2 }
          { (q^0 != '"') then
            (col = col + 1)
            &q^1 }
        } is end
        ntoken &JSTR p &end^1 line col is k
        gs ".s" gn cast nat k gc ':' gs ".string \""
        { &k.p^1 for i (i < &k.p^(k.n - 1)) then gc i@ &i^1 }
        gc '\"' glf
        (x@ = k)
        (p = &p^x.n) }
      (p@ == '\'') ? {
        (col = col + 1)
        &p^1 is q
        (q@ == '\0' and diag line col "unclosed literal byte")
        (q@ == '\\') ?
          { (col = col + 1)
            (q = &q^1)
            (q@ == '\0' and diag line col "unclosed literal byte")
            (q@ == 'a' ) ?'\a'
              (q@ == 'b' ) ?'\b'
              (q@ == 'f' ) ?'\f'
              (q@ == 'n' ) ?'\n'
              (q@ == 'r' ) ?'\r'
              (q@ == 't' ) ?'\t'
              (q@ == 'v' ) ?'\v'
              (q@ == '\'') ? '\''
              (q@ == '\"') ? '\"'
              (q@ == '\\') ? '\\'
                { '\0' ret
                (q@ != '0' and diag line col "invalid escape sequence") } }
      q@ is l
      (col = col + 1)
      (q = &q^1)
      (q@ != '\'' and diag line col "unclosed byte literal")
      ntoken &JCHR p &q^1 line col is k
      (k.l = cast N l)
      (x@ = k)
      (p = &p^x.n) }
      { 0 is n
        { p for q { { isid q@ then (col = col + 1) &q^1 } else cast S 0 ret (n = q - p as N) } }
        !!n ?
            { (x@ = ntoken &JID p &p^n line col)
              (col = col + n)
              (p = &p^n) }
          { read_punct p is n
            !!n ?
                { (x@ = ntoken &JPCT p &p^n line col)
                  (col = col + n)
                  (p = &p^n) }
                diag line col "invalid token" } }
      (x = &x.s) }
    1 }
  decls &k cast O 0
  0
}

ge0     gelfn = {
  gtlit   v "rax"
  0
}

ge1     geefn = {
  geval  e.a c 0
  gop@
  0
}

ge2     geefn = {
  geval  e.a.s  c 0
  gpush        &c "rax"
  geval  e.a    c 0
  gpop         &c "rdi"
  gop@
  0
}

gegneg  gegfn = gtneg  "rax"
geglnot gegfn = glnot  "rax" "eax"
gegadd  gegfn = gadd   "rdi" "rax"
gegsub  gegfn = gsub   "rdi" "rax"
gegmul  gegfn = gmul   "rdi"
gegdiv  gegfn = gdiv
gegmod  gegfn = gmod
gegshr  gegfn = gshr
gegshl  gegfn = gshl
gege    gegfn = ge
gegne   gegfn = gne
gegb    gegfn = gb
gegbe   gegfn = gbe
gega    gegfn = ga
gegae   gegfn = gae

ebrace   exk  = p5 k o
eparen   exk  = p7 k o

enat     exk   = t0 k1 nnat  &gelit
ebyte    exk   = t0 k1 nbyte &gelit
gelit    necl  = ge0 e.k.l

esize    exk   = tt k1 nnat &gesize ptype k o
gesize   necl  = ge0 e.u.size

ederef   exka  = t1 k1 a.t.a &gederef a
gederef  necl  = (gexpr e.a c 0 + 1 - l)

eaddr    exk   = {
  p2 k o is a
  t1 k1 nptr a.t &geaddr a
}
geaddr   necl  = gexpr e.a c 1

eas      exka  = t1 k1 ptype k o &gecast a
ecast    exk   = {
  ptype k o is u
  t1 k1 u &gecast p4 k o
}
gecast  necl  = {
  geval  e.a c 0
  gcast  e.a.t e.t
  0
}

estr   exk  = t0 k1 nptr nbyte &gestr
gestr necl  = {
  gs "mov .s" gn cast nat e.k gs "@GOTPCREL(%rip),%rax" glf
  0
}
eid    exk  = {
  (search_scope k o k1.p k1.n) is s
  (s.is_type and kdiag k@ "not a variable")
  cast E malloc sizeof Expr is e
  (e.s   = 0      )
  (e.gen = &geid  )
  (e.k   = k      )
  s.e ? {
    (e.a = s.e    )
    (e.t = s.e.u  )
  }{(e.a = 0      )
    (e.t = s.t    ) }
  e
}
geid   necl  = {
  (e.a ? !l ? &gloadl &gaddrl &gaddrg)@ e &c "ax"
  0
}

elnot exk  = t1 k1 nnat &gelnot p3    k o   gelnot necl  = ge1 e c l &geglnot
eneg  exk  = t1 k1 nnat &geneg  p3    k o   geneg  necl  = ge1 e c l &gegneg
ediv  exka = t2 k1 nnat &gediv  p4    k o a gediv  necl  = ge2 e c l &gegdiv
emod  exka = t2 k1 nnat &gemod  p4    k o a gemod  necl  = ge2 e c l &gegmod
ee    exka = t2 k1 nnat &gee    psh   k o a gee    necl  = ge2 e c l &gege
ene   exka = t2 k1 nnat &gene   psh   k o a gene   necl  = ge2 e c l &gegne
eae   exka = t2 k1 nnat &geae   psh   k o a geae   necl  = ge2 e c l &gegae
ea    exka = t2 k1 nnat &gea    psh   k o a gea    necl  = ge2 e c l &gega
eb    exka = t2 k1 nnat &geb    psh   k o a geb    necl  = ge2 e c l &gegb
ebe   exka = t2 k1 nnat &gebe   psh   k o a gebe   necl  = ge2 e c l &gegbe
eadd  exka = t2 k1 nnat &geadd  pmul  k o a geadd  necl  = ge2 e c l &gegadd
esub  exka = t2 k1 nnat &gesub  pmul  k o a gesub  necl  = ge2 e c l &gegsub
emul  exka = t2 k1 nnat &gemul  p4    k o a gemul  necl  = ge2 e c l &gegmul
eshl  exka = t2 k1 nnat &geshl  padd  k o a geshl  necl  = ge2 e c l &gegshl
eshr  exka = t2 k1 nnat &geshr  padd  k o a geshr  necl  = ge2 e c l &gegshr

eelem  exka = t2 k1 a.t.a &geelem p3 k o a
geelem necl   = {
  geval  e.a    c 0
  gpush        &c "rax"
  geval  e.a.s  c 0
  gpop         &c "rdx"
  gs  "lea (%rdx,%rax," gn  e.a.t.a.size gs  "),%rax" glf 
  (1 - l)
}

emem  exka  = {
  jadv k
  { a.t for t (t.format == &ftptr) then t.a } is t
  (t.format != &ftstct and kdiag k@ "not a struct")
  cast T 0 is g
  { t.a for a
    { (a.k.n == k1.s.n) then strncmp2 a.k.p k1.s.p k1.s.n }
    ? { (g = a) cast T 0 }
      a.s }
  { !g then kdiag k@ "no such member" }
  t1 k1.s g &gemem a
}
gemem   necl  = {
  geval  e.a c 0
  { e.a.t for t
    (t.format == &ftptr) then
    t.a ret
    gload  t.a.format 0 "ax" "ax" }
  gindex  e.t.offset
  (1 - l)
}

eseq   exka  = {
  p5 k o is b
  t2 k1 b.t &geseq b a
}
geseq  necl  = {
  gdrop  e.a   c
  geval  e.a.s c l
  0
}

eret  exka  = t2 k1 a.t &geret p5 k o a
geret necl  = {
  geval  e.a    c l
  gpush        &c "rax"
  gdrop  e.a.s  c
  gpop         &c "rax"
  0
}

econd  exka = {
  p4 k o is b
  t3 k1 b.t &gecond p4 k o b a
}
gecond necl   = {
  geval   e.a     c 0
  gtcz              "eax"
  gje     e         "a"
  geval   e.a.s   c l
  gj      e         "b"
  glabel  e         "a"
  geval   e.a.s.s c l
  glabel  e         "b"
  0
}

ethen  exka  = {
  p5 k o is b
  t2 k1 b.t &geland b a
}
eland  exka  = {
  pcmp k o is b
  t2 k1 b.t &geland b a
}
geland necl  = {
  (e.a.s.t.format == &ftnone) is keep
  geval  e.a c 0
  gtcz              "eax"
  gje    e          "a"
  { keep then
    gpush        &c "rax" }
  geval  e.a.s c 0
  { keep then
    gpop         &c "rax" }
  glabel  e         "a"
  0
}

eelse  exka  = t2 k1 a.t &gelor p5    k o a
elor   exka  = t2 k1 a.t &gelor pland k o a
gelor  necl  = {
  (e.a.s.t.format == &ftnone) is keep
  geval    e.a   c  0
  gtcz              "eax"
  gjne     e        "a"
  { keep then
    gpush        &c "rax" }
  geval    e.a.s  c 0
  { keep then
    gpop         &c "rax" }
  glabel   e        "a"
  0
}

escratch  exk   = {
  cast E malloc sizeof Expr is e
  ptype k o is u
  scratch Scope o1
  (o1.s  = o         )
  (o1.k  = k@        )
  (o1.e  = e         )
  (o1.is_type = 0    )
  jadv k
  (e.s   = 0         )
  (e.gen = &gescratch)
  (e.k   = k1        )
  (e.u   = u         )
  (e.a   = p5 k &o1  )
  (e.t   = e.a.t     )
  e
}
gescratch necl  = {
  (e.u.size + 7 << 3) is stack
  gfpush    &c stack
  (e.sp = c)
  geval e.a  c l
  gfpop     &c stack
  0
}

eis    exka = {
  cast E malloc sizeof Expr is e
  scratch Scope o1
  (o1.s  = o      )
  (o1.k  = k@    )
  (o1.e  = e      )
  (o1.is_type = 0 )
  jadv k
  (e.s   = 0      )
  (e.gen = &geis  )
  (e.k   = k1     )
  (e.a   = a      )
  (e.u   = a.t    )
  (e.a.s = p5 k &o1)
  (e.t   = e.a.s.t)
  e
}
geis   necl = {
  (e.u.size + 7 << 3) is stack
  geval    e.a    c 0
  gfpush         &c stack
  (e.sp  = c)
  gstoresp e.u
  geval    e.a.s  c l
  gfpop          &c stack
  0
}

efor   exka = {
  cast E malloc sizeof Expr is e
  scratch Scope o1
  (o1.s  = o      )
  (o1.k  = k@    )
  (o1.e  = e      )
  (o1.is_type = 0 )
  jadv k
  (e.s   = 0      )
  (e.gen = &gefor )
  (e.k   = k1     )
  (e.a   = a      )
  (e.u   = a.t    )
  (e.a.s = p5 k &o1)
  (e.t   = e.a.s.t)
  e
}
gefor  necl = {
  (e.u.size + 7 << 3) is stack
  geval     e.a    c 0
  gfpush          &c stack
  (e.sp = c)
  gstoresp  e.u
  gj        e        "b"
  glabel    e        "a"
  gstoresp  e.u
  glabel    e        "b"
  geval     e.a.s  c 0
  gtcz               "eax"
  gjne      e        "a"
  gloadsp   e.u      "rax"
  gfpop           &c stack
  0
}

eass   exka  = t2 k1 nnone &geass p7 k o a
geass  necl  = {
  geval   e.a    c 1
  gpush         &c "rax"
  geval   e.a.s  c 0
  gstore  e.a.t &c
  gtz              "eax"
  0
}

eexit  exk  = t1 k1 nnone &geexit p3 k o
geexit necl = {
  geval e.a c 0
  gt2 "mov" "rax" "rdi"
  gtlit 60 "rax"
  gt0 "syscall"
}

gegsys3 (n N a0 N e E c N) none = {
  geval e.a    c 0
  gpush       &c "rax"
  geval e.a.s  c 0
  gpop        &c "rsi"
  gt2 "mov" "rax" "rdx"
  gtlit a0 "rdi"
  gtlit n "rax"
  gt0 "syscall"
  0
}

eread   exk  = t2 k1 nnone &geread p3 k o p3 k o
geread  necl = gegsys3 0 0 e c

ewrite  exk  = t2 k1 nnone &gewrite p3 k o p3 k o
gewrite necl = gegsys3 1 1 e c

eerr    exk  = t2 k1 nnone &geerr p3 k o p3 k o
geerr   necl = gegsys3 1 2 e c

ftnone ftfn = 0
ftbyte ftfn = 0
ftnat  ftfn = 0
ftstct ftfn = 0
ftfunc ftfn = 0
ftptr  ftfn = 0
